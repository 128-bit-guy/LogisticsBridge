buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}

apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle'

def forge_version = '1.12.2-14.23.5.2854'
def mappings_version = '20171120-1.12'

minecraft {
    mappings channel: 'snapshot', version: mappings_version

    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'


            mods {
                logisticsbridge {
                    sources = [sourceSets.main]
                }
            }
        }

        server {
            workingDirectory project.file('run-server')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'


            mods {
                logisticsbridge {
                    sources = [sourceSets.main]
                }
            }
        }
    }
}


repositories {
    jcenter()
    maven {
        name = 'JitPack.io'
        url = 'https://jitpack.io'
    }
    maven {
        name = "forge"
        url = "https://files.minecraftforge.net/maven"
    }
    flatDir { dirs "./lib" }
}

configurations {
    shade
    implementation.extendsFrom shade
}

dependencies {
    minecraft group: 'net.minecraftforge', name: 'forge', version: forge_version
    compileOnly deobf (":logisticspipes-0.10.3.114")
    runtimeOnly ":logisticspipes-0.10.3.114"
    compileOnly deobf (":MCMultiPart-2.5.3")
    runtimeOnly ":MCMultiPart-2.5.3"
    compileOnly deobf (":ThermalDynamics-1.12.2-2.5.6.1-universal")
    runtimeOnly ":ThermalDynamics-1.12.2-2.5.6.1-universal"
    compileOnly deobf (":refinedstorage-1.6.16")
    runtimeOnly ":refinedstorage-1.6.16"
    compileOnly ":NotEnoughItems-1.12.2-2.4.3.245-deobf"
    compileOnly deobf (":appliedenergistics2-rv6-stable-7")
    runtimeOnly ":appliedenergistics2-rv6-stable-7"
    // https://mvnrepository.com/artifact/javassist/javassist
    shade group: 'javassist', name: 'javassist', version: '3.12.1.GA'
}

tasks.withType(Wrapper) {
    version = 5.1
}

jar {
    configurations.shade.each { dep ->
        from(project.zipTree(dep)) {
            exclude 'META-INF', 'META-INF/**'
        }
    }
}

jar.finalizedBy('reobfJar')