buildscript { 
    ext.configFile = file "build.properties"
    configFile.withReader {
        // Load config.  It shall from now be referenced as simply config or project.config
        def prop = new Properties()
        prop.load(it)
        project.ext.config = new ConfigSlurper().parse prop
    }

    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = 'forge'
            url = 'https://files.minecraftforge.net/maven'
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${config.kotlin_ver}"
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}

apply plugin: 'net.minecraftforge.gradle'

apply plugin: 'java'
apply plugin: 'java-library'

version = "$System.env.MOD_VER"

minecraft {
    mappings channel: 'snapshot', version: "${config.mappings_ver}"

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }

        server {
            workingDirectory project.file('run-server')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }
    }
}


repositories {
    jcenter()
    maven {
        name = 'JitPack.io'
        url = 'https://jitpack.io'
    }
    maven {
        name = 'forge'
        url = 'https://files.minecraftforge.net/maven'
    }
    maven {
        name = 'minecraftlibs'
        url = 'https://libraries.minecraft.net'
    } 
    flatDir { dirs "./lib" }
}

configurations {
    shade
    implementation.extendsFrom shade
}

dependencies {
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: "${config.kotlin_ver}"
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: "${config.kotlin_ver}"
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core' , version: "${config.kotlin_ver}"
    //
    minecraft group: 'net.minecraftforge', name: 'forge', version: "${config.mc_ver}-${config.forge_ver}"
    //
    compileOnly ":logisticspipes-${config.lp_ver}-api"
    compileOnly ":NotEnoughItems-${config.mc_ver}-${config.nei_ver}-deobf"
    implementation ":logisticspipes-${config.lp_ver}-deobf"
    api fg.deobf (":MCMultiPart-${config.mcmp_ver}")
    api fg.deobf (":ThermalDynamics-${config.mc_ver}-${config.td_ver}-universal")
    api fg.deobf (":refinedstorage-${config.rs_ver}")
    api fg.deobf (":appliedenergistics2-${config.ae_ver}")
    //
    runtimeOnly ":refinedstorage-${config.rs_ver}"
    runtimeOnly ":MCMultiPart-${config.mcmp_ver}"
    runtimeOnly ":ThermalExpansion-${config.mc_ver}-${config.te_ver}-universal"
    runtimeOnly ":ThermalFoundation-${config.mc_ver}-${config.tf_ver}-universal"
    runtimeOnly ":CoFHCore-${config.mc_ver}-${config.cofhc_ver}-deobf"
    runtimeOnly ":CoFHWorld-${config.mc_ver}-${config.cofhw_ver}-universal"
    runtimeOnly ":RedstoneFlux-${config.mc_ver_short}-${config.rf_ver}-deobf"
    runtimeOnly ":CodeChickenLib-${config.mc_ver}"
    // https://mvnrepository.com/artifact/javassist/javassist
    shade 'org.javassist:javassist:3.28.0-GA'
    // https://mvnrepository.com/artifact/org.ow2.asm/asm-all
    testImplementation group: 'org.ow2.asm', name: 'asm-all', version: '6.0_BETA'
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "modversion", project.version
    inputs.property "mcversion", "${config.mc_ver}"

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'modversion':project.version, 'mcversion':"${config.mc_ver}"
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
 
jar {
    configurations.shade.each { dep ->
        from(project.zipTree(dep)) {
            exclude 'META-INF', 'META-INF/**'
        }
    }
}

jar.finalizedBy('reobfJar')
